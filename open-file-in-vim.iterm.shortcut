-- https://github.com/normen/vim-macos-scripts/blob/master/open-file-in-vim.applescript
-- opens VIM with file, file list, .vim session file or standalone
-- sets current folder to parent folder of first file
-- Released under MIT license by Normen Hansen

on run {input, parameters}
	set command to {}
	-- check if theres input files at all
	if input is null or input is {} or (item 1 in input) is "" then
		-- no files, open vim without parameters
		set end of command to "vim"
	else
		set firstFile to (item 1 in input)
		-- get parent folder
		set pathFolderParent to characters 1 thru -((offset of "/" in (reverse of items of firstFile as string)) + 1) of firstFile
		set end of command to "cd "
		set end of command to pathFolderParent
		set end of command to ";vim"
		-- support .vim session files
		if firstFile ends with ".vim" then
			set end of command to " -S "
		else
			-- use tabs
			set end of command to " -p "
			--set end of command to " "
		end if
		-- compile all file paths
		set fileList to {}
		repeat with theItem in input
			set end of fileList to quote & theItem & quote & space
		end repeat
		set end of command to (fileList as string)
	end if
	set end of command to ";exit"
	-- compile command
	set command to command as string
	-- 这一句必须放在tell前面，因为在tell里面，可能会将app加载，导致判断不准
	set iterm_running to application "iTerm" is running
	tell application "iTerm"
		activate
		set created to false
		-- 如果没有运行，activate将启动iterm，需要时间，故在此延迟等待
		if not iterm_running then
			repeat 30 times
				if exists current window then
					set created to true
					exit repeat
				end if
				delay 0.2
			end repeat
		end if
		
		if not (exists current window) then
			create window with default profile
			set created to true
		end if
		tell current window
			select
			if not created then
				create tab with default profile
			end if
			tell current session
				write text command
			end tell
		end tell
	end tell
	return input
end run
